#!/usr/bin/env gosh
;;;
;;;   A simple Chaton-Twitter bridge
;;;

;; - Chaton posts are forwarded to a twitter account, truncated if necessary,
;;   plus a back link to the Chaton permalink using bit.ly.
;;
;; - Twitter replies are fowarded to Chaton as a new post.
;;
;; - You need Gauche-net-twitter package.
;;   http://github.com/shirok/Gauche-net-twitter
;;
;; - In order to run this script, you need to have (1) twitter API access
;;   keys (consumer key and consumer secret), (2) twitter access tokens
;;   of the account you're planning to forward messages to, and
;;   (3) a bit.ly account.
;;
;;   You have to provide these info via a configuration file, which contain
;;   an assoc list of the following items:
;;
;;     chaton-url              : chaton's room url to communicate (string)
;;     twitter-username        : twitter username to feed 
;;     twitter-consumer-key    : consumer key (string)
;;     twitter-consumer-secret : consumer secret (string)
;;     twitter-access-token    : access token (string)
;;     twitter-access-token-secret : access token secret (string)
;;     bitly-username     : bit.ly's login name (string)
;;     bitly-apikey       : bit.ly's api key (string)
;;
;;   Example config file entry:
;;
;;     (
;;      (chaton-url       . "http://practical-scheme.net/chaton/chaton")
;;      (twitter-consumer-key . "XXXXXXXXXXXXXXXXXXXX")
;;      (twitter-consumer-secret . "XXXXXXXXXXXXXXXXXXX")
;;      (twitter-access-token . "XXXXXXXXXXXXXXXXXXXXXX")
;;      (twitter-access-token-secret . "XXXXXXXXXXXXXXXXXXX")
;;      (twitter-password . "mytwitterpassword")
;;      (bitly-username   . "mybitlyaccount")
;;      (bitly-apikey     . "mybitlyapikey")
;;     )
;;

(use gauche.threads)
(use gauche.logger)
(use gauche.parseopt)
(use chaton.client)
(use srfi-13)
(use rfc.http)
(use rfc.base64)
(use sxml.ssax)
(use sxml.sxpath)
(use file.util)
(use util.match)
(use util.list)
(use net.twitter)

;;
;; Configuration
;;

(define (read-configuration file)
  (guard (e [(or (<system-error> e)
                 (<read-error> e))
             (exit 1 "Reading config file failed: ~a" (ref e'message))])
    (let1 ss (file->sexp-list file)
      (unless (and (= (length ss) 1) (list? (car ss)))
        (exit 1 "Malformed configuration file: ~a" file))
      (dolist [k '(twitter-username twitter-password bitly-username bitly-apikey)]
        (unless (string? (assoc-ref (car ss) k))
          (exit 1 "Entry for ~a is missing or non-string in ~a" k file)))
      ;; Returns a closure to look up
      (lambda (k) (assoc-ref (car ss) k)))))

;;
;; Bit.ly access layer
;;
(define (shorten-url config url)
  (let1 r (make-request 'get "api.bit.ly"
                        `("/shorten"
                          (version "2.0.1") (format "xml") (longUrl ,url)
                          (login   ,(config 'bitly-username))
                          (apiKey  ,(config 'bitly-apikey))))
    (unless (equal? ((if-car-sxpath '(// statusCode *text*)) r) "OK")
      (log-format "bit.ly returned an error: ~s" r))
    ((if-car-sxpath '(// results nodeKeyVal shortUrl *text*)) r)))

;;
;; Twitter access layer
;;
(define (config->twitter-cred config)
  (make <twitter-cred>
    :consumer-key (config 'twitter-consumer-key)
    :consumer-secret (config 'twitter-consumer-secret)
    :access-token (config 'twitter-access-token)
    :access-token-secret (config 'twitter-access-token-secret)))

(define (max-status-id mentions)
  (if (null? mentions) #f (car (car mentions))))

;;
;; Integration
;;

(define (feed-to-twitter config client nick time text) 
  (let ([content (format "~a: ~a" nick text)]
        [link (shorten-url config (chaton-permalink client time))])
    (log-format "twitter-post: ~a" content)
    (twitter-update
     (config->twitter-cred config)
     (if (> (+ (string-length content) (string-length link) 1) 139)
       (string-append (string-take content (- 138 (string-length link)))
                      (string #\u2026 #\space)
                      link)
       (string-append content " " link)))))

(define (make-observer config)
  (lambda (client message)
    (guard (e [else (log-format "observer error: ~a" (ref e'message)) #f])
      (log-format "~a" message)
      (cond
       [(<chaton-error> message)
        (log-format "chaton-error: ~a" (ref message'message)) #f]
       [(and-let* ([pos (assq-ref message 'pos)]) (< pos (chaton-pos client)))
        #f]
       [(assq-ref message 'content)
        => (lambda (msgs)
             (dolist [m msgs]
               (match m
                 [(nick time text . _)
                  (feed-to-twitter config client nick time text)]
                 [_ #f])))]
       [else #f]))))

(define (forward-from-twitter config client since-id)
  (let* ([cred (config->twitter-cred config)]
         [username (config'twitter-username)]
         [mentions (twitter-mentions cred :since-id since-id)])
    (if (null? mentions)
      since-id
      (let ([followers (twitter-followers/ids cred :screen-name username)]
            [rx (string->regexp #`"@,|username|\\s*")])
        (dolist [m (reverse mentions)]
          (match-let1 (id text user user-id) m
            (when (member user-id followers)
              (chaton-talk client #`",|user|@twitter"
                           (regexp-replace-all rx text "")))))
        (max-status-id mentions)))))

(define (kick-reply-watcher! client config)
  (define timestamp 0)
  (define cred (config->twitter-cred config))
  (define (body)
    (guard (e [else (log-format "watcher error: ~a" (ref e'message))])
      (let loop ((since-id (max-status-id (twitter-mentions cred))))
        (set! timestamp (sys-time))
        (sys-sleep 60)
        (log-format "watcher polling")
        (loop (forward-from-twitter config client since-id))))
    (set! timestamp (sys-time))
    (sys-sleep 60)
    (body))
  (define (watchdog thr)
    (let loop ((n 180))
      (let1 rest (sys-sleep n)
        (unless (zero? rest) (loop rest))))
    (cond [(< timestamp (- (sys-time) 180))
           (log-format "watchdog barks.  restarting watcher thread.")
           (thread-terminate! thr)
           (watchdog (thread-start! (make-thread body)))]
          [else (watchdog thr)]))
  (let1 thr (thread-start! (make-thread body))
    (thread-start! (make-thread (cut watchdog thr)))))

(define (main args)
  (let-args (cdr args) ([logfile "l=s"] [else _ (usage)] . restargs)
    (cond [(equal? logfile "-") (common-log-open #t)]
          [(string? logfile) (common-log-open logfile)])
    (log-format "starting ~a" (car args))
    (match restargs
      [(file)
       (let* ([config (read-configuration file)]
              [client (chaton-connect (config'chaton-url) "chaton-twitter"
                                      (make-observer config))])
         (kick-reply-watcher! client config)
         (while #t (sys-pause)))]
      [_ (usage)])))

(define (usage)
  (print "Usage: chaton-twitter [-l logfile] config-file")
  (exit 0))

(define (common-log-open dst) (log-open dst) (chaton-log-open dst))

;;
;; Utility
;;

(define (make-request method server request-uri :optional body :rest opts)
  (receive (status header body)
      (case method
        [(get)  (apply http-get server request-uri opts)]
        [(post) (apply http-post server request-uri body opts)])
    (unless (equal? status "200")
      (log-format "~a returned status ~a: ~a" server status body)
      (error "make-request error"))
    (call-with-input-string body (cut ssax:xml->sxml <> '()))))

;; Local variables:
;; mode: scheme
;; end:
